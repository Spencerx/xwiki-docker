/*
 * See the NOTICE file distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This is free software; you can redistribute it and/or modify it
 * under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation; either version 2.1 of
 * the License, or (at your option) any later version.
 *
 * This software is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this software; if not, write to the Free
 * Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */
import org.apache.tools.ant.filters.ReplaceTokens

// Run this Gradle build with 'gradle' to generate the various versions and variants from the template directory.
// Whenever a new version of XWiki is out, update this file to update the token values, run gradle and commit the
// result.
//
// Note: As a consequence only update the template files and never the generated files!

defaultTasks 'generate'

def variants = ['mysql-tomcat', 'mariadb-tomcat', 'postgres-tomcat']

// Notes:
// - To compute the XWiki sha256, download the XWiki WAR from
//   http://nexus.xwiki.org/nexus/content/groups/public/org/xwiki/platform/xwiki-platform-distribution-war, and issue:
//   - Unix: sha256sum <binary name>
//   - Mac: shasum --algorithm 256 <binary name>
// - To compute the mysql JDBC sha256, get the JAR at https://search.maven.org/artifact/com.mysql/mysql-connector-j and
//   do the same as for the XWiki sha256.
// - To compute the MariaDB JDBC sha256, get the JAR at
//   https://search.maven.org/artifact/org.mariadb.jdbc/mariadb-java-client and  do the same as for the XWiki sha256.
// - To compute the Postgres JDBC sha256, get the JAR at
//   https://search.maven.org/artifact/org.postgresql/postgresql and do the same as for the XWiki sha256.
// - Use the JDBC driver versions found in the XWiki POM for the specified versions:
//   - mysql: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L110 (link for master)
//   - mariadb: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L113 (link for master)
//   - postgresql: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L119 (link for master)

// Versions of mysql, mariadb and postgres currently in use in the docker images.
def mysql = [
    '9': [
        JDBCVersion: '9.3.0',
        JDBCSha256: '6c8e6692b521376d89bc5618c16cdeaf8c61854329f4fa25677ed08776c5bb76'
    ]
]

def mariadb = [
    '3.5': [
        JDBCVersion: '3.5.3',
        JDBCSha256: '85c4ba2f221d0dfd439c26affbb294f784960763544263c65aba9c2c76858706'
    ]
]

def postgres = [
    '42': [
        JDBCVersion: '42.7.4',
        JDBCSha256: '188976721ead8e8627eb6d8389d500dccc0c9bebd885268a3047180274a6031e'
    ]
]

def tokens = [
    '17': [
        xwikiVersion: '17.4.0',
        xwikiSha256: 'a772a15f5e7e355d11e82fde3c910d23a269e317b208e862f923572520c438b6',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '10',
            javaVersion: '21'
        ]
    ],
    '16': [
        xwikiVersion: '16.10.8',
        xwikiSha256: '559644b36d96d9daf64bec12559899a525e9f6dc164f7c2dcb1ca6c3a0613a62',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '9',
            javaVersion: '21'
        ]
    ]
]

task generate() {
    doLast {
        // Copy the template for all versions and variants
        tokens.keySet().each() { version ->
            variants.each() { variant ->
                // Extract the db type and add it as a token
                def (db, servlet) = variant.tokenize('-')
                tokens[version].'db' = db
                // Copy common template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include '.env'
                    include 'Dockerfile'
                    include 'docker-compose.yml'
                    include 'xwiki/*'
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
                // Copy DB-specific template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include "${db}/*"
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
                // Copy Servlet-specific template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include "${servlet}/*"
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
            }
        }
    }
}
