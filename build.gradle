/*
 * See the NOTICE file distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This is free software; you can redistribute it and/or modify it
 * under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation; either version 2.1 of
 * the License, or (at your option) any later version.
 *
 * This software is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this software; if not, write to the Free
 * Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */
import org.apache.tools.ant.filters.ReplaceTokens

// Run this Gradle build with 'gradle' to generate the various versions and variants from the template directory.
// Whenever a new version of XWiki is out, update this file to update the token values, run gradle and commit the
// result.
//
// Note: As a consequence only update the template files and never the generated files!

defaultTasks 'generate'

def variants = ['mysql-tomcat', 'mariadb-tomcat', 'postgres-tomcat']

// Notes:
// - To compute the XWiki sha256, download the XWiki WAR from
//   http://nexus.xwiki.org/nexus/content/groups/public/org/xwiki/platform/xwiki-platform-distribution-war, and issue:
//   - Unix: sha256sum <binary name>
//   - Mac: shasum --algorithm 256 <binary name>
// - To compute the mysql JDBC sha256, get the JAR at https://search.maven.org/artifact/com.mysql/mysql-connector-j and
//   do the same as for the XWiki sha256.
// - To compute the MariaDB JDBC sha256, get the JAR at
//   https://search.maven.org/artifact/org.mariadb.jdbc/mariadb-java-client and  do the same as for the XWiki sha256.
// - To compute the Postgres JDBC sha256, get the JAR at
//   https://search.maven.org/artifact/org.postgresql/postgresql and do the same as for the XWiki sha256.
// - Use the JDBC driver versions found in the XWiki POM for the specified versions:
//   - mysql: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L110 (link for master)
//   - mariadb: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L113 (link for master)
//   - postgresql: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L119 (link for master)

// Versions of mysql, mariadb and postgres currently in use in the docker images.
def mysql = [
    '9': [
        JDBCVersion: '9.4.0',
        JDBCSha256: '49ed93c8b2bea9cb0929b85a8a28837b191d0f8eac6919fdcef16e36e2cd53b3'
    ]
]

def mariadb = [
    '3.5': [
        JDBCVersion: '3.5.5',
        JDBCSha256: '81b9b10dbbd823e5dc9d81bc48435c76d7e92297a8515cfb75bc620917df9baa'
    ]
]

def postgres = [
    '42': [
        JDBCVersion: '42.7.8',
        JDBCSha256: '2a32a9dcbc42d67a50ad3a0de5efd102c8d2be46720045f2cbd6689f160ab7c7'
    ]
]

def tokens = [
    '17': [
        xwikiVersion: '17.7.0',
        xwikiSha256: '709d599c0312d23e21dedcb69e8756b05c5085caea78c268fc9806f2a3957edc',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '10',
            javaVersion: '21'
        ]
    ],
    '17.4': [
        xwikiVersion: '17.4.4',
        xwikiSha256: 'd810ee6974a8461031c7579ae87969859260a337f78f48d057687fe0edd3d753',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '10',
            javaVersion: '21'
        ]
    ],
    '16': [
        xwikiVersion: '16.10.11',
        xwikiSha256: '9c1d464e35ed8f58f7b47213220db7528cd10732a7428eb9f7ee7c29adc83bbd',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '9',
            javaVersion: '21'
        ]
    ]
]

task generate() {
    doLast {
        // Copy the template for all versions and variants
        tokens.keySet().each() { version ->
            variants.each() { variant ->
                // Extract the db type and add it as a token
                def (db, servlet) = variant.tokenize('-')
                tokens[version].'db' = db
                // Copy common template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include '.env'
                    include 'Dockerfile'
                    include 'docker-compose.yml'
                    include 'xwiki/*'
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
                // Copy DB-specific template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include "${db}/*"
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
                // Copy Servlet-specific template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include "${servlet}/*"
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
            }
        }
    }
}
